apply plugin: 'scala'
apply plugin: 'scalaStyle'

buildscript {
  repositories {
    jcenter() // only work after gradle 1.7
    mavenLocal()
  }

  dependencies {
    classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:2.0.0'
  }
}

sourceSets {
    garbage {
        scala {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    garbageCompile.extendsFrom compile
    garbageRuntime.extendsFrom compile
}

scalaStyle {
    // global config, used in case not overriden specifically
    config = file("$rootDir/scalastyle.xml")
    verbose = false

    sourceSets {
        main {
            // override config
            config = file("$rootDir/scalastyle_main.xml")
        }

        test {
            // override output folder
            output = file("$rootDir/scalastyle-test-result.xml")
        }

        garbage {
            // do not even create scalastyle task for this source set
            skip = true
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.11'
}
